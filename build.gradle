plugins {
    id 'java'
    id 'java-library'
}

group = 'six.eared.macaque'

ext {
    projectVersion = '1.0.0'
    logbackVersion = '1.2.3'
    fastjsonVersion = '2.0.6'
    projectUrl = 'https://github.com/haiyanghan/macaque-hotswap'
}


allprojects {
    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        [compileJava]*.options*.encoding = 'UTF-8'
    }
    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        [compileTestJava]*.options*.encoding = 'UTF-8'
    }
}

subprojects {
    repositories {
        mavenCentral()
        // snapshot
        maven {
            url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    def publish = ['macaque-mbean', 'macaque-common'].contains(project.name)
    if (publish) {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        publishing {
            repositories {
                maven {
                    name 'releaseRepository'
                    url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username = "${MVN_USER_NAME}"
                        password = "${MVN_PASSWORD}"
                    }
                }
                maven {
                    name = 'snapshotRepository'
                    url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    credentials {
                        username = "${MVN_USER_NAME}"
                        password = "${MVN_PASSWORD}"
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    afterEvaluate {
                        ["srcJar", "docJar"].forEach { taskName ->
                            def task = tasks.findByName(taskName)
                            if (task != null)
                                artifact task
                        }
                        from components.java
                        groupId = 'io.github.haiyanghan'
                        version = projectVersion
                        artifactId = 'six-eared-' + archivesBaseName
                        pom {
                            name = project.name
                            url = "${projectUrl}"
                            licenses {
                                license {
                                    name = 'The Apache License, Version 2.0'
                                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                }
                            }

                            scm {
                                connection = "scm:git:${projectUrl}.git"
                                developerConnection = "scm:git:${projectUrl}.git"
                                url = "${projectUrl}"
                            }
                        }
                    }
                }
            }

            task docJar(type: Jar, dependsOn: javadoc) {
                archiveClassifier = 'javadoc'
                from javadoc.destinationDir
            }

            javadoc {
                options.charSet = 'UTF-8'
                options.docEncoding = 'UTF-8'
                options.encoding = 'UTF-8'
                options.memberLevel = JavadocMemberLevel.PROTECTED
                options.addStringOption('Xdoclint:none', '-quiet')

                // The URL structure was changed to include the locale after Java 8
                if (JavaVersion.current().isJava11Compatible())
                    options.links "https://docs.oracle.com/en/java/javase/${JavaVersion.current().majorVersion}/docs/api/"
                else
                    options.links "https://docs.oracle.com/javase/8/docs/api/"
            }
        }

        if (!projectVersion.endsWith("SNAPSHOT")) {
            signing {
                sign publishing.publications.mavenJava
            }
        }
    }
}